# Datatypes
println(weather);             # Object (builtin only)
println("string");            # String
println(42);                  # Number
println(3.14159265);          # Number (supports floats)
println(true);                # Booleans
println(false);               # Booleans
println("key" => "value");    # Pairs (1 dimensional hash-maps)
println(println);               # Functions

# The `let` keyword can be used for variable declaration
let string_value = "any string";
string_value = 1 as str;    # Once a variable is bound to a value, it must not change its type
# string_value = 1;         # Illegal assignment
let _object_value = weather;

# on / off are synonyms for true / false
let boolean_value = true;
boolean_value = on;
boolean_value = off;

let number_value = 42;
number_value = 3.14159265;  # Homescript supports re-assignment
number_value +=  1;         # Plus assignment
number_value -=  2;         # Minus assignment
number_value *=  3;         # Multiply assignment
number_value /=  4;         # Divide assignment
number_value %=  5;         # Reminder assignment
number_value **= 6;         # Power assignment
println("after", number_value);

# The exec function calls another Homescript
let exec_result = exec(
    "test_script",          # The target HMS ID
    "key" => 42,            # Arguments to the HMS are in pair-form
    #...                    # Many more arguments (or even none) are legal
);
println(                      # The result is an object
    exec_result.elapsed,    # The elapsed runtime (in seconds)
    exec_result.value       # The scripts return value (value of last statement)
);

# String concatenation using different types is allowed
# Every variable wich can be printlned can be used in concatenation
println(
    "Pi is roughly " + "a value of " + 3.14159265 + " right?"
);
# However, operations involving completely different types are illegal
#println(1 + false);

# Error handling
try {
    throw("error");         # Throw- and Runtime errors can be caught
} catch err {
    println(err);
};

try {
    # This is a type error and can therefore not be caught
    # 1 + false;
} catch err {
    println(err);
};

# Homescript supports anonymous functions
let a = fn () {
    println("Anonymous function");
};
a();

# Program control flow
if true == false {
    throw("This should be unreachable");
} else if 1 > 0 {
    println("Else if works like this");
} else {
    println("Final else block");
};

# Loops
for any_identifier in 0..10  {
    println(any_identifier);
};

let while_counter = 0;
while while_counter < 10 {
    while_counter += 1;
};

let loop_counter = 0;
let last_loop_counter = loop {
    if loop_counter > 10 {
        break loop_counter;
    };
    loop_counter += 1;
};
println("Last loop counter:", last_loop_counter);

# Homescript supports a very basic module system
# Functions can be imported from another script
# import foo [ as bar ] from baz;

# Imports the fib(n) function
#import fib from fibonacci;

# Calculate some fibonacci numbers
#for n in 2..22 {
    #println("fib(" + n + ") = " + fib(n));
#};

# Imports the fizzbuzz function
#import fizz_buzz from fizzbuzz;

# Play fizzbuzz until 32
#fizz_buzz(32);

# Imprts the primes function
#import primes from primes;

# Calculate 100 primes
#primes(100);

# Imports the box function
#import box from box;

#for size in 3..50 {
    #print(box(size));
#};
#for size in 50..3 {
    #print(box(size));
#};
